TINY BASIC EXAMPLE PROGRAMS
---------------------------

Here are some Tiny Basic programs that will work on the TEC-1F Version of Tiny Basic.
Type them in, learn and play around with the code!


## Fibonacci Sequence
```
 5 REM ** FIBONACCI SEQUENCE **
10 PRINT "FIBONACCI SEQUENCE"
20 FOR I=1 TO 22
30 GOSUB 70
40 PRINT "F",I,F
50 NEXT I
60 STOP
70 LET A=0; LET B=1
80 FOR J=1 TO I
90 LET T=A+B; LET A=B; LET B=T
100 NEXT J
110 LET F=A
120 RETURN
```

## NIM (Matches) Game
```
 5 REM ** NIM GAME **
10 PRINT "NIM"
20 LET H=21
30 PRINT "THERE ARE",
40 PRINT #3,H,
50 PRINT " TOKENS REMAINING, HOW MANY WOULD YOU LIKE?"
60 INPUT "TOKENS" T
70 IF T>3 GOTO 180
80 IF T<1 GOTO 180
90 LET H=H-T
100 IF H=0 GOTO 200
105 REM CHANGE 110 TO LET T=RND(3) IF YOU HAVE A CHANCE TO WIN
110 LET T=4-T
120 PRINT "I WILL TAKE",
130 PRINT #2,T,
140 PRINT " TOKENS"
150 LET H=H-T
160 IF H=0 GOTO 220
170 GOTO 30
180 PRINT "YOU MUST TAKE 1, 2 OR 3 TOKENS ONLY!"
190 GOTO 60
200 PRINT "I WIN!!!, YOU GOT THE LAST TOKEN!"
210 STOP
220 PRINT "YOU GOT THE LAST TOKEN, CONGRATULATIONS"
```

## Factors of N
```
 5 REM ** FACTORS OF N **
10 INPUT "GIVE ME A NUMBER" I
20 LET C=1
30 PRINT "FACTORS OF ",#3,I,":"
40 IF I/C*C=I PRINT C
50 C=C+1
60 IF C<=I GOTO 40
70 GOTO 10
```

## Print ASCII Characters
```
10 REM ** PRINT ASCII CHARACTERS **
20 FOR I=0 TO 127-32
30 PRINT #3,&I+32,$32,$I+32,$32,
40 IF (I+1)/10*10=(I+1) PRINT
50 NEXT I
```

## Dump Memory Contents
```
  5 REM ** MEMORY DUMP **
 10 INPUT "ADDRESS" A
 20 FOR I=0 TO 15
 30 P=A+I*16
 40 PRINT &P,":",
 50 FOR J=0 TO 15
 60 PRINT $32,&PEEK(P+J),
 70 NEXT J
 80 PRINT " |",
 90 FOR J=0 TO 15
100 PRINT %PEEK(P+J),
110 NEXT J
120 PRINT "|"
130 NEXT I
140 INPUT "MORE ? (0 TO EXIT)" Q
150 IF Q=0 STOP
160 A=A+256
170 GOTO 20
```

## TEC Seven Segment Output Demo
```
 5 REM ** SEGMENT OUTPUT DEMO **
10 @(0)=235;@(1)=40;@(2)=205;@(3)=173;@(4)=46
20 @(5)=167;@(6)=231;@(7)=41;@(8)=239;@(9)=175
30 XOFF
40 OUT 1,1
50 FOR I=0 TO 9
60 OUT 2,@(I)
70 FOR J=1 TO 1000;NEXT J
80 NEXT I
90 XON
```

## TEC Multiplex Demo
_Note_: It's slow because TB is slow...
```
  5 REM ** SEGMENT MULTIPLEX DEMO *
 10 @(0)=44;@(1)=107;@(2)=66;@(3)=195;@(4)=199;@(5)=70
 20 XOFF
 30 A=0;B=32
 40 OUT 2,@(A)
 50 OUT 1,B
 60 FOR I=1 TO 15;NEXT I
 70 OUT 1,0
 80 IF B=1 GOTO 30
 90 A=A+1;B=B/2
100 GOTO 40
```

## Moonlander
```
10 PRINT "MOONLANDER"
30 T=0;H=500;V=50;F=120
40 PRINT "TIME:",T," HEIGHT",H
50 PRINT "VEL.:",V," FUEL",F
60 IF F=0 GOTO 100
70 INPUT "BURN? (1-30)" B
80 IF B<1 B=1
90 IF B>30 B=30
100 IF B>F B=F
110 W=V-B+5;F=F-B
120 IF (W+V)/2>=H GOTO 150
130 H=H-((W+V)/2);T=T+1;V=W
140 GOTO 40
150 IF W>5 PRINT "YOU CRASHED ALL DEAD"
160 IF W>1 IF W<=5 PRINT "OK-BUT SOME INJURIES"
170 IF W<=1 PRINT "GOOD LANDING"
```

## TIC TEC TOE
A Game by B Chiha.
```
  1 REM *** TIC TEC TOE ***
  2 REM *** GAME BY B. CHIHA ***
  5 GOSUB 360
 10 INPUT "PLAY FIRST OR SECOND? (1 OR 2)" H
 20 IF H<1 GOTO 10
 30 IF H>2 GOTO 10
 40 IF H=1 C=2
 50 IF H=2 C=1
 60 P=1
 70 GOSUB 300
 80 IF P=H GOSUB 200
 90 IF P#H GOSUB 260
100 G=G-1
110 GOSUB 410 
120 IF W=0 IF G=0 W=3
130 IF W#0 GOTO 160
140 IF P=1 P=2; GOTO 70
150 IF P=2 P=1; GOTO 70
160 GOSUB 300
170 IF W=H PRINT "CONGRATULATIONS, YOU WIN!!!"
180 IF W=C PRINT "COMPUTER WINS, HAHA!!"
190 IF W=3 PRINT "IT'S A DRAW!!"
195 STOP
199 REM PLAYER MOVE
200 PRINT "PLACE AN '",$@(H+10),
205 INPUT "' WHERE?" M
210 IF M<1 GOTO 200
220 IF M>9 GOTO 200
230 IF @(M)>57 PRINT "ALREADY TAKEN!";GOTO 200
240 @(M) = @(H+10)
250 RETURN
259 REM COMPUTER MOVE
260 R = RND(9)
270 IF @(R)>57 GOTO 260
280 PRINT "COMPUTER PLACES AN '",$@(C+10),"' AT ",#1,R
290 @(R) = @(C+10)
295 RETURN
299 REM DISPLAY BOARD
300 PRINT;PRINT " ",#2,$@(1)," | ",$@(2)," | ",$@(3)
310 PRINT "---+---+---"
320 PRINT " ",#2,$@(4)," | ",$@(5)," | ",$@(6)
330 PRINT "---+---+---"
340 PRINT " ",#2,$@(7)," | ",$@(8)," | ",$@(9);PRINT
350 RETURN
359 REM INITIALISE BOARD
360 FOR I=1 TO 9
370 @(I) = I+48
380 NEXT I
390 @(11)=88; @(12)=79
400 W=0;G=9
405 RETURN
409 REM CHECK FOR WINNER
410 FOR Q=1 TO 2
420 IF @(1)+@(2)+@(3)=291-(27*Q) W=Q
430 IF @(4)+@(5)+@(6)=291-(27*Q) W=Q
440 IF @(7)+@(8)+@(9)=291-(27*Q) W=Q
450 IF @(1)+@(4)+@(7)=291-(27*Q) W=Q
460 IF @(2)+@(5)+@(8)=291-(27*Q) W=Q
470 IF @(3)+@(6)+@(9)=291-(27*Q) W=Q
480 IF @(1)+@(5)+@(9)=291-(27*Q) W=Q
490 IF @(3)+@(5)+@(7)=291-(27*Q) W=Q
500 NEXT Q
510 RETURN
```


