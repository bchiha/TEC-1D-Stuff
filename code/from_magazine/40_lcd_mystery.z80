; LCD Mystery effect
; To be used via the DAT board and its connections
; Port 4 -> Enable
; A7 -> Register Select
; R/W -> Read / Write
; D0-7 -> D0-7

            ORG    0A00H

;Port 4 on TEC is connected to LCD Enable
;A7 is connected to  Register select.  A7=0 for Instruction, A7=1 for Data
LCD_IR:     EQU    00000100B
LCD_DR:     EQU    10000100B ;A7 is Data Register
LCDOFF:     EQU    0821H
LCDRST:     EQU    08FFH

START:
            ; JMON config
            LD     A,0FFH
            LD     (LCDOFF),A       ;Disables LCD update from JMON
            LD     A,0AAH
            LD     (LCDRST),A       ;Disables hard reset and sets PC to 0x0A00

            ; LCD set up
            RST    30H              ;Check for LCD Busy
            LD     A,01H            ;Display Clear
            OUT    (LCD_IR),A       ;Send to Instruction Register (IR)
            RST    30H              ;Check for LCD Busy
            LD     A,06H            ;Increment Cursor from Left to Right
            OUT    (LCD_IR),A       ;Send to Instruction Register (IR)
            RST    30H              ;Check for LCD Busy
            
            ; Send multiple characters using lookup table and OUTI
            LD     BC,1000H | LCD_DR  ;B=10 (letters), C=84 (port number)
            LD     HL,TABLE         ;LCD table lookup
LOOP1:
            RST    30H              ;Check for LCD Busy
            OUTI                    ;Send (HL) to port C, Decrement B, Increment HL
            JR     NZ, LOOP1        ;OUTI sets zero if B=0;

            ; Tell the LCD that next data will be to CGRAM
            RST    30H              ;Check for LCD Busy
            LD     A,40H            ;CGRAM entry
            OUT    (LCD_IR),A       ;Send to Instruction Register (IR)

            ; Save multiple characters to CGRAM using lookup table and OUTI
            LD     B,40H            ;B=40 (letters), C=84 (port number)
            LD     HL,TABLE+20H     ;LCD table lookup
LOOP2:
            RST    30H              ;Check for LCD Busy
            OUTI                    ;Send (HL) to port C, Decrement B, Increment HL
            JR     NZ, LOOP2        ;OUTI sets zero if B=0;

            ; Set cursor to second row
            RST    30H              ;Check for LCD Busy
            LD     A,0C0H           ;Set Cursor to Second line
            OUT    (LCD_IR),A       ;Send to Instruction Register (IR)

            ; Send multiple characters using lookup table and OUTI
            LD     B,10H            ;B=10 (letters)
            LD     HL,TABLE+10H     ;LCD table lookup
LOOP3:
            RST    30H              ;Check for LCD Busy
            OUTI                    ;Send (HL) to port C, Decrement B, Increment HL
            JR     NZ, LOOP3        ;OUTI sets zero if B=0;

            HALT

TABLE:
            DB     20H,4DH,49H,52H,52H,4FH,52H,20H    ;It's a Mystery!
            DB     49H,4DH,41H,47H,45H,21H,20H,20H

            DB     20H,00H,01H,02H,02H,03H,02H,20H
            DB     01H,00H,04H,05H,06H,07H,20H,20H

            DB     00H,11H,11H,11H,15H,15H,1BH,11H
            DB     00H,0EH,04H,04H,04H,04H,04H,0EH
            DB     00H,11H,12H,14H,1EH,11H,11H,1EH
            DB     00H,0EH,11H,11H,11H,11H,11H,0EH
            DB     00H,11H,11H,1FH,11H,11H,11H,0EH
            DB     00H,0FH,11H,11H,17H,10H,11H,0EH
            DB     00H,1FH,10H,10H,1EH,10H,10H,1FH
            DB     00H,04H,00H,00H,04H,04H,04H,04H
